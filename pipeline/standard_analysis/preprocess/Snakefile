configfile: srcdir('config.yaml')
include: config['snake_dir'] + '/common/preambule.snake'

preprocess = extract_from_config(['reads', 'preprocess'], {})
read_types_to_keep = [read_type for read_type in preprocess.keys() if not preprocess[read_type].get('temporary', True)]
read_types_with_fastqc_report = extract_from_config(['reads', 'report', 'fastqc'], [])

localrules: finalise__preprocess_reads_and_report

rule finalise__preprocess_reads_and_report:
    """
    Pipeline filter and revise fastq files from the reads/original directory. All preprocess steps, such as trimming,
    deduplication and filtering of contamination reads are defined in the configuration file, part 'preprocess'.
    Preprocess steps are executed gradually, i.e. output reads of one step are input to the following step.
    """
    input:
        # Fastq files of all preprocess types that are not configured as temporary
        reads   = expand('reads/{read_type}/{sample}_R{orientation}.fastq.gz',
                          read_type=read_types_to_keep, sample=pipeline.sample, orientation=[1,2]),

        # FastQC quality reports of all preprocess types that are configured to be generated
        fastqcs = expand('reads/{read_type}/stats/{sample}_R{orientation}_fastqc.html',
                          read_type=read_types_with_fastqc_report, sample=pipeline.sample,
                          orientation=[1,2]),

        # Summary table of fastQC reports
        reports = expand('reads/{read_type}/stats/summary.html',
                          read_type=read_types_with_fastqc_report)

    output:
        # FastQC quality reports of all preprocess types that are configured to be generated
        fastqcs = expand('{report_dir}/read_quality_report/{read_type}/{sample}_R{orientation}_fastqc.html',
                          report_dir=config['report_dir'], read_type=read_types_with_fastqc_report,
                          sample=pipeline.sample, orientation=[1,2]),

        # Summary table of fastQC reports
        reports = expand('{report_dir}/read_quality_report/{read_type}/index.html',
                          report_dir=config['report_dir'], read_type=read_types_with_fastqc_report)
    run:
        copy_input_files_with_consistent_output_names(input, output)