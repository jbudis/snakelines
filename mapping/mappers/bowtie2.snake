include: srcdir('indices/bowtie2_index.snake')

rule bowtie2_map_reads:
    input:
        r1 = 'reads/%s/{sample}_R1.fastq.gz' % config['mapping']['read_type'],
        r2 = 'reads/%s/{sample}_R2.fastq.gz' % config['mapping']['read_type'],
        index = 'reference/{reference}/bowtie2_index/{reference}.1.bt2',
        reffasta = 'reference/{reference}/{reference}.fa'
    output:
        bam = 'bowtie2/{reference}/{sample}.bam'
    log:
        'bowtie2/{reference}/log/{sample}.map'
    params:
        index = 'reference/{reference}/bowtie2_index/{reference}',
        tmpdir = 'bowtie2/{reference}/tmp/',
        additional = config['mapping']['params'] if 'params' in config['mapping'] else '',
        regions = config['mapping']['regions'] if 'regions' in config['mapping'] else '',
        min_map_quality = config['mapping']['min_map_quality'] if 'min_map_quality' in config['mapping'] else 0
    benchmark:
        'bowtie2/{reference}/benchmark/{sample}.txt'
    threads:
        int(config['threads'])
    shell:
        '''
        hostname > {log}.host;
        REGION_PARAM=
        if [ -n "{params.regions}" ]; then
            REGION_PARAM="-L reference/{wildcards.reference}/annotation/{params.regions}/regions.bed";
        fi

        bowtie2 \
            -x {params.index} \
            -1 {input.r1} \
            -2 {input.r2} \
            --threads {threads} \
            {params.additional} -t \
        2> {log} \
        | samtools view \
            -b \
            -q {params.min_map_quality} \
            --reference {input.reffasta} \
            --threads {threads} \
            $REGION_PARAM \
            - \
        | samtools sort \
            --threads {threads} \
            -o {output.bam} \
            -T {params.tmpdir} \
            -m 5G \
            - \
        '''

rule bowtie2_remap_reads_to_assembly:
    input:
        r1 = 'reads/%s/{sample}_R1.fastq.gz' % config['mapping']['read_type'],
        r2 = 'reads/%s/{sample}_R2.fastq.gz' % config['mapping']['read_type'],
        index = '{fapath}/{sample}/bowtie2_index/{contigs}.1.bt2',
        fasta = '{fapath}/{sample}/{contigs}.fa'
    output:
        bam = '{fapath}/{sample}/mapping/{contigs}.bam'
    log:
        '{fapath}/{sample}/mapping/log/{sample}.map'
    params:
        index = '{fapath}/{sample}/bowtie2_index/{contigs}',
        tmpdir = '{fapath}/{sample}/tmp/',
        additional = config['mapping']['params'] if 'params' in config['mapping'] else ''
    threads:
        int(config['threads'])
    shell:
        '''
        bowtie2 \
            -x {params.index} \
            -1 {input.r1} \
            -2 {input.r2} \
            --threads {threads} \
            {params.additional} -t \
        2> {log} \
        | samtools view \
            -b \
            --threads {threads} \
            - \
        | samtools sort \
            --threads {threads} \
            -o {output.bam} \
            -T {params.tmpdir} \
            -m 5G \
            - \
        '''
