SNAKELINES_VERSION = '0.8.0'

import os
import sys
from src.config.dependency_load import load_config, check_dependency
from src.output.copy_files import copy_input_files_with_consisent_names_dict
from src.output.copy_files import copy_config
from src.output.copy_files import store_snakelines_version
from src.output.send_email import send_email

# Load configuration file if not already loaded
config = load_config(config)

if 'threads' not in config:
    config['threads'] = 1

# get the snakelines_directory
if 'snakelines_dir' not in config:
    config['snakelines_dir'] = os.path.dirname(os.path.realpath(workflow.snakefile))

# Run startup scripts that automatically loads imports from src/ and load helper methods
include: config['snakelines_dir'] + '/src/common.snake'

# check dependencies and asses inputs and outputs
outputs, copy_where = check_dependency(config['snakelines_dir'] + '/src/dependency.yaml', config)

# function to evaluate output directories from dependency.yaml
def get_files(source_dict):
    additional_params = {'expand': expand, 'protected': protected, 'temp': temp, 'config': config,
            'pipeline': pipeline, 'directory': directory, 'read_types_with_quality_report': read_types_with_quality_report,
            'multisample_references': multisample_references, 'tax_levels_count_table': tax_levels_count_table,
            'tax_levels_barplot': tax_levels_barplot, 'barplot_formats': barplot_formats,
            'tax_levels_alphas': tax_levels_alphas,'map_types_with_quality_report': map_types_with_quality_report}

    files_dict = {}
    for k,v in source_dict.items():
        try:
            files_dict[k] = eval(v, additional_params)
        except (NameError, SyntaxError):
            files_dict[k] = v

    return files_dict

# "pipeline" rule that generates all of the required files
rule pipeline:
    input:
        **get_files(outputs)

# on success copy the files to report directory and send email(s)
onsuccess:
    if 'report_dir' in config:
        print("Copying and finalizing", file=sys.stderr)
        copied = copy_input_files_with_consisent_names_dict(get_files(outputs), get_files(copy_where), pipeline)
        print("Copied {} files to report dir '{}'".format(copied, config['report_dir']), file=sys.stderr)
        copy_config(config['report_dir'], workflow, pipeline)
        store_snakelines_version(config['report_dir'], SNAKELINES_VERSION)
    send_email(config, get_files(outputs), get_files(copy_where), True)

# on error send email(s)
onerror:
    send_email(config, get_files(outputs), get_files(copy_where), False)
