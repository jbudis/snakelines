include: config['snake_dir'] + '/reference/indices/faidx.snake'

rule mpileup_germline:
    input:
        bam = '{mapper}/{reference}/{sample}.dup.realign.bam',
        bai = '{mapper}/{reference}/{sample}.dup.realign.bam.bai',
        bed = 'reference/{reference}/annotation/{panel}/regions.bed',
        reffasta = 'reference/{reference}/{reference}.fa',
        reffaidx = 'reference/{reference}/{reference}.fa.fai'
    output:
        '{mapper}/{reference}/mpileup/{sample}.{panel}.germline.vcf'
    shell:
        '''
        samtools mpileup \
            --max-depth 10000 \
            --min-BQ 20 \
            --positions {input.bed} \
            --uncompressed \
            --fasta-ref {input.reffasta} \
            --output-tags DP,AD,ADF,ADR,SP,INFO/AD,INFO/ADF,INFO/ADR \
            {input.bam} \
        | bcftools call \
            --consensus-caller \
            --output-type v \
            - \
        > {output}
        '''

rule mpileup_population:
    input:
        bam = '{mapper}/{reference}/{sample}.bam',
        bai = '{mapper}/{reference}/{sample}.bam.bai',
        reffasta = 'reference/{reference}/{reference}.fa',
        reffaidx = 'reference/{reference}/{reference}.fa.fai'
    output:
        '{mapper}/{reference}/mpileup/{sample}.population.vcf'
    shell:
        '''
        samtools mpileup \
            --max-depth 10000 \
            --min-BQ 20 \
            --uncompressed \
            --fasta-ref {input.reffasta} \
            --output-tags DP,AD,ADF,ADR,SP,INFO/AD,INFO/ADF,INFO/ADR \
            {input.bam} \
        | bcftools call \
            --consensus-caller \
            --output-type v \
            - \
        > {output}
        '''


rule filter_variants:
    input:
        '{mapper}/{reference}/mpileup/{sample}.population.vcf'
    output:
        '{mapper}/{reference}/mpileup/{sample}.population.filter.vcf'
    params:
        min_nonref_allele_freq = config['variant']['min_nonref_allele_freq'],
        min_coverage = config['variant']['min_coverage'],
        keep_indels = '' if config['variant']['keep_indels'] else '--remove-indels'
    shell:
        '''
        vcftools \
            --non-ref-af {params.min_nonref_allele_freq} \
            --recode  \
            --vcf {input} \
            --minDP {params.min_coverage} \
            --stdout \
            {params.keep_indels} \
        > {output}
        '''