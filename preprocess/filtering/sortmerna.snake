rule sortmerna_interleave_paired_reads:
    input:
        r1 = 'reads/%s/{sample}_R1.fastq' % config['sortmerna']['read_type'],
        r2 = 'reads/%s/{sample}_R2.fastq' % config['sortmerna']['read_type']
    output:
        rl = 'reads/%s/{sample}_RL.fastq' % config['sortmerna']['read_type']
    shell:
        '''
        sortmerna_merge-paired-reads \
            {input.r1} \
            {input.r2} \
            {output.rl}
        '''

rule sortmerna_index:
    input:
        ref = 'reference/{reference}/{reference}.fa'
    output:
        index = 'reference/{reference}/sortmerna/{reference}.idx.stats'
    params:
        tmpdir = 'reference/{reference}/sortmerna',
        index_prefix = 'reference/{reference}/sortmerna/{reference}.idx',
    shell:
        '''
        sortmerna_index \
            --ref {input.ref},{params.index_prefix} \
            --tmpdir {params.tmpdir}
        '''


rule sortmerna_filter_reads:
    input:
        rl = 'reads/%s/{sample}_RL.fastq' % config['sortmerna']['read_type'],
        ref = 'reference/{reference}/{reference}.fa',
        index = 'reference/{reference}/sortmerna/{reference}.idx.stats'
    output:
        rl = 'reads/sortmerna-{reference}/{sample}_RL.fastq'
    params:
        prefix = 'reads/sortmerna-{reference}/{sample}',
        index_prefix = 'reference/{reference}/sortmerna/{reference}.idx',
        output_prefix = 'reads/sortmerna-{reference}/{sample}_RL'
    threads:
        int(config['threads'])
    shell:
        '''
        sortmerna \
            --ref {input.ref},{params.index_prefix} \
            --reads {input.rl} \
            --aligned {params.output_prefix} \
            --fastx \
            --paired_in \
            -a {threads}
        '''


rule sortmerna_uninterleave_paired_reads:
    input:
        rl = 'reads/sortmerna-{reference}/{sample}_RL.fastq'
    output:
        r1 = 'reads/sortmerna-{reference}/{sample}_R1.fastq.gz',
        r2 = 'reads/sortmerna-{reference}/{sample}_R2.fastq.gz'
    params:
        r1 = 'reads/sortmerna-{reference}/{sample}_R1.fastq',
        r2 = 'reads/sortmerna-{reference}/{sample}_R2.fastq'
    shell:
        '''
        sortmerna_unmerge-paired-reads \
            {input.rl} \
            {params.r1} \
            {params.r2}

        gzip {params.r1}
        gzip {params.r2}
        '''