rule prokka__find_and_annotate_genes:
    """
    Annotates and find genes across assembled contigs.
    :input contigs: fasta file with de novo assembled contigs from reads
    :output gff: GFF3 file containing sequences and annotations
    :output faa: protein fasta file of the translated CDS sequences
    :output ffn: nucleotide fasta file of all the prediction transcripts
    :output tbl: feature table type 
    :params outdir: output folder
    :params prefix: filename output prefix
    """
    input:
        contigs     = 'assembly/{sample}/contigs.fa'
    output:
        gff         = 'annotation/{sample}/{sample}.gff',
        faa         = 'annotation/{sample}/{sample}.faa',
        ffn         = 'annotation/{sample}/{sample}.ffn',
        tbl         = 'annotation/{sample}/{sample}.tbl'
    log:
        out         = 'annotation/{sample}/log.out', # log report from fast virome explorer tool
        err         = 'annotation/{sample}/log.err'  # error report from fast virome explorer tool
    params:
        outdir      = 'annotation/{sample}',                  # ouput directory, necessary for fast virome explorer
        prefix      = '{sample}'
    threads:
        int(config['threads'])
    conda:
        config['snakelines_dir'] + '/enviroments/prokka.yaml'
    shell:
        '''
        prokka \
            --outdir {params.outdir} \
            --prefix {params.prefix} \
            --addgenes \
            --kingdom Viruses \
            --metagenome \
            --cpus {threads} \
            --force \
            --mincontiglen 200 \
            --norrna \
            --notrna \
            {input.contigs} \
         > {log.out} \
        2> {log.err}
        '''