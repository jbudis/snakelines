include: config['snakelines_dir'] + '/rules/reference/annotation/regions.snake'
include: config['snakelines_dir'] + '/rules/mapping/index/samtools.snake'
include: config['snakelines_dir'] + '/rules/reference/index/fai/samtools.snake'

rule gatk__call_germline_variants:
    """
        Detect variants within the mapped reads and annotates them with dbSNP
        if possible when the database is provided.
    """
    input:
        bam         = 'mapping/{{reference}}/{map_type}/{{sample}}.bam'.format(map_type=pipeline.postprocessed_map_type),
        bai         = 'mapping/{{reference}}/{map_type}/{{sample}}.bam.bai'.format(map_type=pipeline.postprocessed_map_type),
        bed         = 'reference/{reference}/annotation/{panel}/regions.interval_list',
        fasta       = 'reference/{reference}/{reference}.fa',
        fai         = 'reference/{reference}/{reference}.fa.fai',
    output:
        vcf         = 'variant/{reference}-{panel}/{state}/{sample}.vcf'
    params:
        dbsnp       = 'reference/{reference}/variants/dbsnp.vcf.bgz',
        dbsnp_index = 'reference/{reference}/variants/dbsnp.vcf.bgz.tbi',
    threads:
        int(config['threads'])
    log:
        out = 'variant/{reference}-{panel}/{state}/log/{sample}.vcf.out',
        err = 'variant/{reference}-{panel}/{state}/log/{sample}.vcf.err'
    shell:
        """
        DBSNP=
        if [ -f {params.dbsnp} -a -f {params.dbsnp_index} ]; then
            DBSNP='--dbsnp {params.dbsnp}'
        fi

        gatk HaplotypeCaller \
            --input {input.bam} \
            --reference {input.fasta} \
            --output {output.vcf} \
            --intervals {input.bed} \
            $DBSNP \
            --native-pair-hmm-threads {threads} \
        1> {log.out} \
        2> {log.err}
        """

