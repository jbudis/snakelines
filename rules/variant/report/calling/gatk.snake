rule gatk__fix_vcf_header:
    """
    Adds missing sequence dictionary to VCF header. This job also generates VCF index (.idx).
    :input vcf: Raw VCF from variant caller vardict.
    :input fasta: Reference sequence.
    :output vcf: Fixed VCF.
    :output vcf_index: Fixed VCF index.
    """
    input:
        vcf = 'variant/{reference}-{panel}/original/{sample}.vardict.vcf',
        fasta = 'reference/{reference}/{reference}.fa'
    output:
        vcf = 'variant/{reference}-{panel}/original/{sample}.vcf',
        vcf_index = 'variant/{reference}-{panel}/original/{sample}.vcf.idx',
    log:
        out = 'variant/{reference}-{panel}/original/log/{sample}.out',
        err = 'variant/{reference}-{panel}/original/log/{sample}.err',
    conda:
        config['snakelines_dir'] + '/enviroments/others_env.yaml'
    shell:
        """
        gatk SelectVariants \
            -V {input.vcf} \
            -R {input.fasta} \
            -O {output.vcf} \
            1>{log.out} \
            2>{log.err}
        """

rule tabix__index_vcf:
    """
    Create tabix intex on BGZF (bgzipped) VCF file.
    :input: bgzipped vcf
    :output: tabix index of bgzipped VCF file
    """
    input:
        '{path}/{filename}.vcf.bgz'
    output:
        '{path}/{filename}.vcf.bgz.tbi'
    log:
        out = '{path}/log/{filename}_tabix.out',
        err = '{path}/log/{filename}_tabix.err'
    conda:
        config['snakelines_dir'] + '/enviroments/others_env.yaml'
    shell:
        """
        tabix -p vcf {input} 1>{log.out} 2>{log.err}
        """

rule gatk__collect_variant_calling_metrics:
    """
    GATK tool for generating
    :input vcf: called variants
    :input vcf_index: index of called variants
    :input dbsnp: DBSNP in BGZF format written by bgzip.
    :input dbsnp_index: DBSNP tabix index.
    :input intervals: Genomic regions of interest.
    :output filename: Text file with summary of calling metrics.
    """
    input:
        vcf = 'variant/{reference}-{panel}/original/{sample}.vcf',
        vcf_index = 'variant/{reference}-{panel}/original/{sample}.vcf.idx',
        dbsnp = 'reference/{reference}/variants/dbsnp.vcf.bgz',
        dbsnp_index = 'reference/{reference}/variants/dbsnp.vcf.bgz.tbi',
        intervals = 'reference/{reference}/annotation/{panel}/regions.interval_list',
    output:
        filename = 'variant/{reference}-{panel}/original/calling_metrics/{sample}.variant_calling_summary_metrics'
    log:
        out = 'variant/{reference}-{panel}/original/calling_metrics/log/{sample}.out',
        err = 'variant/{reference}-{panel}/original/calling_metrics/log/{sample}.err'
    conda:
        config['snakelines_dir'] + '/enviroments/others_env.yaml'
    shell:
        """
        gatk CollectVariantCallingMetrics \
            --INPUT {input.vcf} \
            --DBSNP {input.dbsnp} \
            --OUTPUT variant/{wildcards.reference}-{wildcards.panel}/original/calling_metrics/{wildcards.sample} \
            --THREAD_COUNT {threads} \
            --TARGET_INTERVALS {input.intervals} \
            1> {log.out} \
            2> {log.err}
        """