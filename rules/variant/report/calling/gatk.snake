rule gatk__fix_vcf_header:
    '''
    add missing sequence dictionary to vcf header
    this job also generates vcf index (.idx)
    '''
    input:
        vcf = 'mapping/{reference}/{map_type}/{caller}-{panel}/{sample}.vcf',
        fasta = 'reference/{reference}/{reference}.fa'
    output:
        vcf = 'mapping/{reference}/{map_type}/{caller}-{panel}/{sample}.header_fix.vcf',
        vcf_index = 'mapping/{reference}/{map_type}/{caller}-{panel}/{sample}.header_fix.vcf.idx'
    log:
        out = 'mapping/{reference}/{map_type}/{caller}-{panel}/{sample}/log/fix_vcf_header.out',
        err = 'mapping/{reference}/{map_type}/{caller}-{panel}/{sample}/log/fix_vcf_header.err'
    shell:
        '''
        gatk4 SelectVariants \
            -V {input.vcf} \
            -R {input.fasta} \
            -O {output.vcf} \
            1>{log.out} \
            2>{log.err}
        '''

rule gatk__index_dbsnp:
    '''
    generates .idx index file for DBSNP VCF
    '''
    input:
        dbsnp = 'reference/{reference}/dbsnp/dbsnp.vcf',
        fasta = 'reference/{reference}/{reference}.fa'
    output:
        dbsnp = 'reference/{reference}/dbsnp/dbsnp.fixed.vcf',
        dbsnp_index = 'reference/{reference}/dbsnp/dbsnp.fixed.vcf.idx',
    log:
        out = 'reference/{reference}/dbsnp/log/index.out',
        err = 'reference/{reference}/dbsnp/log/index.err'
    shell:
        '''
        gatk4 SelectVariants \
            -V {input.dbsnp} \
            -R {input.fasta} \
            -O {output.dbsnp} \
            1>{log.out} \
            2>{log.err}
        '''


#rule bgzip__bgzip_vcf:
#    input:
#        'mapping/{reference}/{map_type}/{caller}-{panel}/{sample}.header_fix.vcf'
#    output:
#        'mapping/{reference}/{map_type}/{caller}-{panel}/{sample}.header_fix.vcf.gz'
#    shell:
#        '''
#        bgzip -c {input} > {output}
#        '''


#rule tabix__index_vcf:
#    input:
#        'mapping/{reference}/{map_type}/{caller}-{panel}/{sample}.header_fix.vcf.gz'
#    output:
#        'mapping/{reference}/{map_type}/{caller}-{panel}/{sample}.header_fix.vcf.gz.tbi'
#    log:
#        out = 'mapping/{reference}/{map_type}/{caller}-{panel}/{sample}/log/index_vcf.out',
#        err = 'mapping/{reference}/{map_type}/{caller}-{panel}/{sample}/log/index_vcf.err'
#    shell:
#        '''
#        tabix -p vcf {input} 1>{log.out} 2>{log.err}
#        '''

rule picard__bed_to_interval_list:
    input:
        bed = 'reference/{reference}/annotation/wgs/regions.bed',
        seq_dict = 'reference/{reference}/{reference}.dict'
    output:
        intervals = 'mapping/{reference}/{map_type}/{caller}-{panel}/{sample}.interval_list'
    log:
        out = 'mapping/{reference}/{map_type}/{caller}-{panel}/{sample}/log/interval_list.out',
        err = 'mapping/{reference}/{map_type}/{caller}-{panel}/{sample}/log/interval_list.err'
    shell:
        '''
        picard BedToIntervalList \
            INPUT={input.bed} \
            SEQUENCE_DICTIONARY={input.seq_dict} \
            OUTPUT={output.intervals} \
            SORT=true \
            UNIQUE=true \
            1>{log.out} \
            2>{log.err}
        '''

rule gatk__collect_variant_calling_metrics:
    input:
        vcf = 'mapping/{reference}/{map_type}/{caller}-{panel}/{sample}.header_fix.vcf',
        vcf_index = 'mapping/{reference}/{map_type}/{caller}-{panel}/{sample}.header_fix.vcf.idx',
        dbsnp = 'reference/{reference}/dbsnp/dbsnp.fixed.vcf',
        dbsnp_index = 'reference/{reference}/dbsnp/dbsnp.fixed.vcf.idx',
        intervals = 'mapping/{reference}/{map_type}/{caller}-{panel}/{sample}.interval_list'
    output:
        filename = 'mapping/{reference}/{map_type}/{caller}-{panel}/report/calling/{sample}.variant_calling_summary_metrics'
    log:
        out = 'mapping/{reference}/{map_type}/{caller}-{panel}/report/calling/log/{sample}.out',
        err = 'mapping/{reference}/{map_type}/{caller}-{panel}/report/calling/log/{sample}.err'
    shell:
        '''
        gatk4 CollectVariantCallingMetrics \
            --INPUT {input.vcf} \
            --DBSNP {input.dbsnp} \
            --OUTPUT mapping/{wildcards.reference}/{wildcards.map_type}/{wildcards.caller}-{wildcards.panel}/report/calling/{wildcards.sample} \
            --THREAD_COUNT {threads} \
            --TARGET_INTERVALS {input.intervals} \
            1>{log.out} \
            2>{log.err}
        '''