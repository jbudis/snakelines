rule gatk__fix_vcf_header:
    '''
    add missing sequence dictionary to vcf header
    this job also generates vcf index (.idx)
    '''
    input:
        vcf = 'variant/{reference}-wgs/original/{sample}.vardict.vcf',
        fasta = 'reference/{reference}/{reference}.fa'
    output:
        vcf = 'variant/{reference}-wgs/original/{sample}.vcf',
        vcf_index = 'variant/{reference}-wgs/original/{sample}.vcf.idx',
    log:
        out = 'variant/{reference}-wgs/original/log/{sample}.out',
        err = 'variant/{reference}-wgs/original/log/{sample}.err',
    shell:
        '''
        gatk4 SelectVariants \
            -V {input.vcf} \
            -R {input.fasta} \
            -O {output.vcf} \
            1>{log.out} \
            2>{log.err}
        '''

#rule gatk__index_dbsnp:
#    '''
#    generates .idx index file for DBSNP VCF
#    '''
#    input:
#        dbsnp = 'reference/{reference}/dbsnp/dbsnp.vcf',
#        fasta = 'reference/{reference}/{reference}.fa'
#    output:
#        dbsnp = 'reference/{reference}/dbsnp/dbsnp.fixed.vcf',
#        dbsnp_index = 'reference/{reference}/dbsnp/dbsnp.fixed.vcf.idx',
#    log:
#        out = 'reference/{reference}/dbsnp/log/index.out',
#        err = 'reference/{reference}/dbsnp/log/index.err'
#    shell:
#        '''
#        gatk4 SelectVariants \
#            -V {input.dbsnp} \
#            -R {input.fasta} \
#            -O {output.dbsnp} \
#            1> {log.out} \
#            2> {log.err}
#        '''

rule picard__bed_to_interval_list:
    input:
        bed      = 'reference/{reference}/annotation/wgs/regions.bed',
        seq_dict = 'reference/{reference}/{reference}.dict'
    output:
        intervals = 'variant/{reference}-wgs/original/interval_list/{sample}.interval_list'
    log:
        out = 'variant/{reference}-wgs/original/interval_list/log/{sample}.out',
        err = 'variant/{reference}-wgs/original/interval_list/log/{sample}.err'
    shell:
        '''
        picard BedToIntervalList \
            INPUT={input.bed} \
            SEQUENCE_DICTIONARY={input.seq_dict} \
            OUTPUT={output.intervals} \
            SORT=true \
            UNIQUE=true \
            1> {log.out} \
            2> {log.err}
        '''

rule gatk__collect_variant_calling_metrics:
    input:
        vcf = 'variant/{reference}-{panel}/original/{sample}.vcf',
        vcf_index = 'variant/{reference}-{panel}/original/{sample}.vcf.idx',

        dbsnp = 'reference/{reference}/dbsnp/dbsnp.vcf',
        dbsnp_index = 'reference/{reference}/dbsnp/dbsnp.vcf.idx',

        intervals = 'variant/{reference}-{panel}/original/interval_list/{sample}.interval_list'
    output:
        filename = 'variant/{reference}-{panel}/original/calling_metrics/{sample}.variant_calling_summary_metrics'
    log:
        out = 'variant/{reference}-{panel}/original/calling_metrics/log/{sample}.out',
        err = 'variant/{reference}-{panel}/original/calling_metrics/log/{sample}.err'
    shell:
        '''
        gatk4 CollectVariantCallingMetrics \
            --INPUT {input.vcf} \
            --DBSNP {input.dbsnp} \
            --OUTPUT variant/{wildcards.reference}-{wildcards.panel}/original/calling_metrics/{wildcards.sample} \
            --THREAD_COUNT {threads} \
            --TARGET_INTERVALS {input.intervals} \
            1> {log.out} \
            2> {log.err}
        '''