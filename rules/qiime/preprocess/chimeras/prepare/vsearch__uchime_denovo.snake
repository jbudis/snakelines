rule qiime__vsearch__uchime_denovo:
    """
    Finds chimeric and nonchimeric seqeunces
    :input table: qiime artifact of type FeatureTable[Frequency], e.g. reads/qiime/joined-table-w-chimeras.qza
    :input rep_seqs: qiime artifact of type FeatureData[Sequence], e.g. reads/qiime/joined-rep-w-chimeras-seqs.qza
    :params dn: [FLOAT] No vote pseudo-count, corresponding to the parameter n in the chimera scoring function. [default: 1.4]
    :params mindiffs: [INTEGER RANGE] Minimum number of differences per segment. [default: 3]
    :params mindiv: [FLOAT] Minimum divergence from closest parent. [default: 0.8]
    :params minh: [FLOAT] Minimum score (h). Increasing this value tends to reduce the number of false positives and to decrease sensitivity. [default: 0.28]
    :params xn: [FLOAT] No vote weight, corresponding to the parameter beta in the scoring function. [default: 8.0]
    :params uchime_out: DO NOT CHANGE! output directory for chimeras.qza and nonchimeras.qza
    """
    input:
        table     = 'reads/qiime/{name}-table-w-chimeras.qza',
        rep_seqs  = 'reads/qiime/{name}-rep-w-chimeras-seqs.qza'
    output:
        chimeras    = 'reads/qiime/{name}-uchime-dn-out/chimeras.qza',
        nonchimeras = 'reads/qiime/{name}-uchime-dn-out/nonchimeras.qza'
    params:
        dn        = '--p-dn {}'.format(method_config['dn']) if method_config.get('dn', '') != '' else '',
        mindiffs  = '--p-mindiffs {}'.format(method_config['mindiffs']) if method_config.get('mindiffs', '') != '' else '',
        mindiv    = '--p-mindiv {}'.format(method_config['mindiv']) if method_config.get('mindiv', '') != '' else '',
        minh      = '--p-minh {}'.format(method_config['minh']) if method_config.get('minh', '') != '' else '',
        xn        = '--p-xn {}'.format(method_config['xn']) if method_config.get('xn', '') != '' else '',
        uchime_out   = 'reads/qiime/{name}-uchime-dn-out',
        tmp_dir   = method_config['tmp_dir'] if method_config.get('tmp_dir', '') != '' else '/tmp'
    shell:
        '''
        rm -rf {params.uchime_out}
        export TMPDIR={params.tmp_dir}; qiime vsearch uchime-denovo \
              --i-table {input.table} \
              --i-sequences {input.rep_seqs} \
              {params.dn} \
              {params.mindiffs} \
              {params.mindiv} \
              {params.minh} \
              {params.xn} \
              --output-dir {params.uchime_out}
        '''
