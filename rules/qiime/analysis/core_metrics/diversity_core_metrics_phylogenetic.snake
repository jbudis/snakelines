rule qiime__diversity_core_metrics_phylogenetic:
    """
    Performs QIIME 2’s diversity analyses through the q2-diversity plugin.

    Calls 'qiime diversity core-metrics-phylogenetic'.

    Supports computing alpha and beta diversity metrics, applys related statistical tests, and generate interactive visualizations. First apply the core-metrics-phylogenetic method, which rarefies a FeatureTable[Frequency] to a user-specified depth, computes several alpha and beta diversity metrics, and generates principle coordinates analysis (PCoA) plots using Emperor for each of the beta diversity metrics. The metrics computed by default are:

    * Alpha diversity
        * Shannon’s diversity index (a quantitative measure of community richness)
        * Observed OTUs (a qualitative measure of community richness)
        * Faith’s Phylogenetic Diversity (a qualitiative measure of community richness that incorporates phylogenetic relationships between the features)
        * Evenness (or Pielou’s Evenness; a measure of community evenness)

    * Beta diversity
        * Jaccard distance (a qualitative measure of community dissimilarity)
        * Bray-Curtis distance (a quantitative measure of community dissimilarity)
        * unweighted UniFrac distance (a qualitative measure of community dissimilarity that incorporates phylogenetic relationships between the features)
        * weighted UniFrac distance (a quantitative measure of community dissimilarity that incorporates phylogenetic relationships between the features)

    An important parameter for this script is --p-sampling-depth, which is the even sampling (i.e. rarefaction) depth. In this pipeline, sampling depth is set as Minimum of sequences count (e.g. we have 3 samples, first with 3500 sequences, 2nd with 3000 sequences and 3rd with 4500 sequences). Minimum is 3000. Value is taken from exported qzv file, exactly from reads/qiime/joined-imported/overview.html.

    Note:
    Because most diversity metrics are sensitive to different sampling depths across different samples, this script will randomly subsample the counts from each sample to the value provided for this parameter. For example, if you provide --p-sampling-depth 500, this step will subsample the counts in each sample without replacement so that each sample in the resulting table has a total count of 500. If the total count for any sample(s) are smaller than this value, those samples will be dropped from the diversity analysis. Choosing this value is tricky.

    :input html: html export of imported.qza file, e.g. reads/qiime/joined-imported/index.html
    :input rooted_tree: rooted tree, qiime artifact of type Phylogeny[Rooted], e.g. reads/qiime/joined-rooted-tree-dn-99.qza
    :input table: qiime artifact of type FeatureTable[Frequency], e.g. reads/qiime/joined-table.qza
    :input metadata: samples' metadata filepath in TSV format (tab-separated value), e.g. description/joined-sample-metadata.tsv

    :output rarefield_table: qiime artifact of type FeatureTable[Frequency], e.g. reads/qiime/joined-core-metrics-results/rarefied_table.qza
    :output vector: sample data for alpha diversity, qiime artifact of type SampleData[AlphaDiversity], e.g. reads/qiime/joined-core-metrics-results/shannon_vector.qza
    :output distance_matrix: distance matrix for beta diversity, qiime artifact of type DistanceMatrix, e.g. reads/qiime/joined-core-metrics-results/bray_curtis_distance_matrix.qza
    :output emperor_plots: Emperor visualization of distance matrix, e.g reads/qiime/joined-core-metrics-results/bray_curtis_emperor.qzv

    :params output_dir: DO NOT CHANGE, dirpath to core-metrics-results directory
    :params report_dir: DO NOT CHANGE, dirpath to overview.html
    """
    params:
        output_dir  = 'reads/qiime/{name}-core-metrics-results',
        report_dir  = 'reads/qiime/{name}-imported'
    input:
        html        = 'reads/qiime/{name}-imported/index.html',
        rooted_tree = expand('reads/qiime/{{name}}-rooted-tree{params_file_suffix}.qza', params_file_suffix = '-dn-{}'.format(method_config['similarity']) if method_config.get('similarity', '') != '' else ''),
        table       = expand('reads/qiime/{{name}}-table{params_file_suffix}.qza', params_file_suffix = '-dn-{}'.format(method_config['similarity']) if method_config.get('similarity', '') != '' else ''),
        metadata    = 'description/{name}-sample-metadata.tsv'
    output:
        rarefield_table = 'reads/qiime/{name}-core-metrics-results/rarefied_table.qza',
        vector          = expand('reads/qiime/{{name}}-core-metrics-results/{alpha_types}_vector.qza', alpha_types=['evenness', 'faith_pd', 'observed_otus', 'shannon']),
        distance_matrix = expand('reads/qiime/{{name}}-core-metrics-results/{beta_types}_distance_matrix.qza', beta_types=['bray_curtis', 'jaccard', 'unweighted_unifrac', 'weighted_unifrac']),
        emperor_plots   = expand('reads/qiime/{{name}}-core-metrics-results/{emperor_types}_emperor.qzv', emperor_types=['bray_curtis', 'jaccard', 'unweighted_unifrac', 'weighted_unifrac']),
        pcoa_results    = expand('reads/qiime/{{name}}-core-metrics-results/{beta_types}_pcoa_results.qza', beta_types=['bray_curtis', 'jaccard', 'unweighted_unifrac', 'weighted_unifrac'])
    threads:
        int(config['threads'])
    shell:
        '''
        rmdir {params.output_dir}
        min="cat {params.report_dir}/overview.html | egrep 'Minimum' | sed -E 's/[^0-9]*([0-9]*)[^0-9]*/\\1/'"
        min=$(eval $min)
        qiime diversity core-metrics-phylogenetic \
          --i-phylogeny {input.rooted_tree} \
          --i-table {input.table} \
          --p-sampling-depth $min \
          --p-n-jobs {threads} \
          --m-metadata-file {input.metadata} \
          --output-dir {params.output_dir}
        '''
