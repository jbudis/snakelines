include: srcdir('indices/bwa_index.snake')

rule bwa__map_reads_to_reference:
    """
    For input preprocessed reads bwa finds the most similar genomic region in the provided reference genome, using bwa mem algorithm.
    :input r1: gzipped fastq file with left reads, e.g. 'reads/%s/{sample}_R1.fastq.gz'
    :input r2: gzipped fastq file with right reads, e.g. 'reads/%s/{sample}_R2.fastq.gz'
    :input index: reference index (created by rule bwa__prepare_index), e.g. 'reference/{reference}/bwa_index/{reference}.bwt'
    :output bam: mapped read in BAM file, e.g. 'mapping/{reference}/original/{sample}.bam'
    :param index: name of reference, technically filename's path prefix, e.g. 'reference/{reference}/bwa_index/{reference}'
    :param additional: additional params
    :param concordant: more strict conditions for scoring options should help to map only concordant reads
    """
    input:
        r1    = 'reads/%s/{sample}_R1.fastq.gz' % pipeline.preprocessed_read_type,
        r2    = 'reads/%s/{sample}_R2.fastq.gz' % pipeline.preprocessed_read_type,
        index = 'reference/{reference}/bwa_index/{reference}.bwt',
    output:
        bam   = 'mapping/{reference}/original/{sample}.bam'
    log:
        err   = 'mapping/{reference}/original/log/{sample}.bwa.map_stats'
    params:
        index = 'reference/{reference}/bwa_index/{reference}',
        additional = method_config.get('bwa_params', ''),
        concordant = '-B 40 -O 60 -E 10  â€“L100' if method_config.get('only_concordant', False) else ''
    threads:
        int(config['threads'])
    conda:
        config['snakelines_dir'] + '/enviroments/others_env.yaml'
    shell:
        '''
        bwa mem \
            -t {threads} \
            {params.index} \
            {input.r1} \
            {input.r2} \
            {params.concordant} \
            {params.additional} \
        2> {log.err} \
        | samtools view -bS - \
        > {output.bam}
        '''