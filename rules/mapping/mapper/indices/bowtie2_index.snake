rule bowtie2__prepare_index:
    """
    Generate mapping index that is utilized by Bowtie2 algorithm to map reads to a reference genome
    :input fa: fasta reference genome
    :output indices: 6 files with indeces. e.g '{fadir}/bowtie2_index/{sequence}.[1-4].bt2', '{fadir}/bowtie2_index/{sequence}.rev.[1,2].bt2'
    :param index: name of output reference, technically filename's path prefix, e.g. '{fadir}/bowtie2_index/{sequence}'
    """
    input:
        '{fadir}/{sequence}.fa'
    output:
        '{fadir}/bowtie2_index/{sequence}.1.bt2',
        '{fadir}/bowtie2_index/{sequence}.2.bt2',
        '{fadir}/bowtie2_index/{sequence}.3.bt2',
        '{fadir}/bowtie2_index/{sequence}.4.bt2',
        '{fadir}/bowtie2_index/{sequence}.rev.1.bt2',
        '{fadir}/bowtie2_index/{sequence}.rev.2.bt2'
    params:
        index = '{fadir}/bowtie2_index/{sequence}'
    log:
        out   = '{fadir}/bowtie2_index/log/{sequence}.log',
        err   = '{fadir}/bowtie2_index/log/{sequence}.err',
    threads:
        int(config['threads'])
    conda:
        config['snakelines_dir'] + '/enviroments/others_env.yaml'
    shell:
        """
        bowtie2-build \
            --threads {threads} \
            {input} \
            {params.index} \
        >  {log.out} \
        2> {log.err}
        """
