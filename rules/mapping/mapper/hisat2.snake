include: srcdir('indices/hisat2_index.snake')

rule hisat2__map_reads_to_reference:
    """
    For input preprocessed reads from RNA-seq hisat2 finds the most similar genomic region in the provided reference genome.
    The method accounts for specifics of RNA-seq read mapping, such as splice junctions.
    :input r1: gzipped fastq file with left reads, e.g. 'reads/%s/{sample}_R1.fastq.gz'
    :input r2: gzipped fastq file with right reads, e.g. 'reads/%s/{sample}_R2.fastq.gz'
    :input index: reference index (created by rule hisat2__prepare_index), e.g. 'reference/{reference}/hisat2_index/{reference}.1.bt2'
    :input ref: reference genome, e.g. 'reference/{reference}/{reference}.fa'
    :output bam: mapped read in BAM file, e.g. 'mapping/{reference}/original/{sample}.bam'
    :param index: name of reference, technically filename's path prefix, e.g. 'reference/{reference}/hisat2_index/{reference}'
    :param additional: additional params
    :param concordant: only concordant reads
    """
    input:
        r1    = 'reads/%s/{sample}_R1.fastq.gz' % pipeline.preprocessed_read_type,
        r2    = 'reads/%s/{sample}_R2.fastq.gz' % pipeline.preprocessed_read_type,
        index = 'reference/{reference}/hisat2_index/{reference}.1.ht2',
        ref   = 'reference/{reference}/{reference}.fa'
    output:
        bam   = 'mapping/{reference}/original/{sample}.bam'
    log:
        err   = 'mapping/{reference}/original/log/{sample}.hisat2.map_stats'
    params:
        index = 'reference/{reference}/hisat2_index/{reference}',
        additional = method_config.get('params', ''),
        concordant = '--no-mixed --no-discordant' if method_config.get('only_concordant', False) else ''
    threads:
        int(config['threads'])
    conda:
        config['snakelines_dir'] + '/enviroments/others_env.yaml'
    shell:
        """
        hisat2 \
            -x {params.index} \
            -1 {input.r1} \
            -2 {input.r2} \
            --threads {threads} \
            {params.concordant} \
            {params.additional} \
        2> {log.err} \
        | samtools view -bS - \
        > {output.bam}
        """