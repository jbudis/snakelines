include: config['snakelines_dir'] + '/rules/reference/annotation/regions.snake'
include: config['snakelines_dir'] + '/rules/reference/index/fai/samtools.snake'
include: config['snakelines_dir'] + '/rules/mapping/index/samtools.snake'

rule gatk__recalibrate_pass1:
    """
        Generates first pass recalibration table for Base Quality Score Recalibration (BQSR).
    """
    input:
        bam         = 'mapping/{{reference}}/{map_type}/{{sample}}.bam'.format(map_type=method_config['input_map_type']),
        bai         = 'mapping/{{reference}}/{map_type}/{{sample}}.bam.bai'.format(map_type=method_config['input_map_type']),
        fasta       = 'reference/{reference}/{reference}.fa',
        fai         = 'reference/{reference}/{reference}.fa.fai',
        dbsnp       = 'reference/{reference}/variants/dbsnp.vcf.bgz',
        dbsnp_index = 'reference/{reference}/variants/dbsnp.vcf.bgz.tbi',
        # TODO panel
        bed         = 'reference/{reference}/annotation/twist/regions.interval_list'
    output:
        table = 'mapping/{reference}/recalibrated/{sample}.table.pass1'
    log:
        out = 'mapping/{reference}/recalibrated/log/{sample}.table.pass1.out',
        err = 'mapping/{reference}/recalibrated/log/{sample}.table.pass1.err'
    threads:
        int(config['threads'])
    shell:
        """
        gatk BaseRecalibrator \
            --input {input.bam} \
            --reference {input.fasta} \
            --known-sites {input.dbsnp} \
            --intervals {input.bed} \
            --output {output.table} \
        1> {log.out} \
        2> {log.err}
        """

rule gatk__recalibrate_pass2:
    """
        Generates second pass recalibration table for Base Quality Score Recalibration (BQSR) validation.
        @see https://gatkforums.broadinstitute.org/gatk/discussion/44/base-quality-score-recalibration-bqsr
    """
    input:
        bam         = 'mapping/{reference}/recalibrated/{sample}.bam',
        bai         = 'mapping/{reference}/recalibrated/{sample}.bam.bai',
        fasta       = 'reference/{reference}/{reference}.fa',
        fai         = 'reference/{reference}/{reference}.fa.fai',
        dbsnp       = 'reference/{reference}/variants/dbsnp.vcf.bgz',
        dbsnp_index = 'reference/{reference}/variants/dbsnp.vcf.bgz.tbi',
        # TODO panel
        bed         = 'reference/{reference}/annotation/twist/regions.interval_list'
    output:
        table = 'mapping/{reference}/recalibrated/{sample}.table.pass2'
    log:
        out = 'mapping/{reference}/recalibrated/log/{sample}.table.pass2.out',
        err = 'mapping/{reference}/recalibrated/log/{sample}.table.pass2.err'
    threads:
        int(config['threads'])
    shell:
        """
        gatk BaseRecalibrator \
            --input {input.bam} \
            --reference {input.fasta} \
            --known-sites {input.dbsnp} \
            --intervals {input.bed} \
            --output {output.table} \
        1> {log.out} \
        2> {log.err}
        """

rule gatk__apply_bqsr:
    """
        Apply base quality score recalibration.
    """
    input:
        bam     = 'mapping/{{reference}}/{map_type}/{{sample}}.bam'.format(map_type=method_config['input_map_type']),
        bai     = 'mapping/{{reference}}/{map_type}/{{sample}}.bam.bai'.format(map_type=method_config['input_map_type']),
        fasta   = 'reference/{reference}/{reference}.fa',
        fai     = 'reference/{reference}/{reference}.fa.fai',
        table   = 'mapping/{reference}/recalibrated/{sample}.table.pass1',
         # TODO panel
        bed     = 'reference/{reference}/annotation/twist/regions.interval_list'
    output:
        bam = configured_temp('mapping/{reference}/recalibrated/{sample}.bam')
    log:
        out = 'mapping/{reference}/recalibrated/log/{sample}.bam.out',
        err = 'mapping/{reference}/recalibrated/log/{sample}.bam.err'
    threads:
        int(config['threads'])
    shell:
        """
        gatk ApplyBQSR \
            --input {input.bam} \
            --reference {input.fasta} \
            --bqsr-recal-file {input.table} \
            --intervals {input.bed} \
            --output {output.bam} \
        1> {log.out} \
        2> {log.err}
        """
