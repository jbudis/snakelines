rule unicycler__assemble_reads_into_contigs:
    """
    Assemble preprocessed reads into the larger genomic sequences, contigs. Also generate contig overlap graphs and
    initial scaffolds. Use 'unicycler' program.
    :input r1: gzipped fastq file with left reads
    :input r2: gzipped fastq file with right reads
    :output gfa: assembly graph in .gfa format as needed by downstream analysis
    :output contigs: assembled contigs in .fa file
    :param outdir: output directory (do not change)
    :param contigs: contig file generated by spades (do not change)
    :param gfa: assembly graph in .gfa format as generated by unicycler
    """
    input:
        r1 = 'reads/%s/{sample}_R1.fastq.gz' % pipeline.preprocessed_read_type,
        r2 = 'reads/%s/{sample}_R2.fastq.gz' % pipeline.preprocessed_read_type
    output:
        gfa     = 'assembly/{sample}/assembly_graph.gfa',
        contigs = 'assembly/{sample}/contigs.fa'
    params:
        outdir  = 'assembly/{sample}',
        gfa     = 'assembly/{sample}/assembly.gfa',
        contigs = 'assembly/{sample}/assembly.fasta'
    threads:
        int(config['threads'])
    log:
        out = 'assembly/{sample}/log/unicycler.log',
        err = 'assembly/{sample}/log/unicycler.err'
    conda:
        config['snakelines_dir'] + '/enviroments/unicycler.yaml'
    shell:
        """
        unicycler \
            -1 {input.r1} \
            -2 {input.r2} \
            -o {params.outdir} \
            -t {threads} \
            --spades_path spades \
        >  {log.out} \
        2> {log.err}

        # Unite outputs of Unicycler with outputs of Spades
        sed 's/\ /_/g' {params.contigs} > {output.contigs}
        rm {params.contigs}

        mv {params.gfa} {output.gfa}
        """
