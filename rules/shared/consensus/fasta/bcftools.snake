include: config['snakelines_dir'] + '/rules/shared/variant/compression/bgzip.snake'
include: config['snakelines_dir'] + '/rules/shared/variant/index/tabix.snake'
include: config['snakelines_dir'] + '/rules/shared/reference/index/fai/samtools.snake'
include: config['snakelines_dir'] + '/rules/shared/mapping/bed/bedtools.snake'

rule bcftools__create_consensus_fasta:
    """
        Create consensus sequence by applying VCF variants to a reference fasta file.
        By default, the program will apply all ALT variants to the reference fasta to obtain the consensus sequence.
        See http://samtools.github.io/bcftools/bcftools.html#consensus
    """
    input:
        vcf      = 'variant/{reference}-{panel}/original/{sample}.vcf',
        vcf_gz   = 'variant/{reference}-{panel}/original/{sample}.vcf.gz',
        tbi      = 'variant/{reference}-{panel}/original/{sample}.vcf.gz.tbi',
        fasta    = 'reference/{reference}/{reference}.fa',
        fai      = 'reference/{reference}/{reference}.fa.fai',
        genome   = 'reference/{reference}/{reference}.genome',
        bedgraph = 'mapping/{reference}/%s/bed/{sample}.bedgraph' % pipeline.postprocessed_map_type
    output:
        fasta = 'consensus/{reference}-{panel}/{sample}.fa'
    log:
        out = 'consensus/{reference}-{panel}/log/{sample}.fa.out',
        err = 'consensus/{reference}-{panel}/log/{sample}.fa.err'
    params:
        mask_lte_coverage = method_config.get('mask_lte_coverage', -1),
        coverage_bed = 'consensus/{reference}-{panel}/{sample}.coverage.bed',
        complement_bed = 'consensus/{reference}-{panel}/{sample}.complement.bed',
        deletions_bed = 'consensus/{reference}-{panel}/{sample}.deletions.bed',
        masking_bed = 'consensus/{reference}-{panel}/{sample}.masking.bed',
        use_iupac = 'true' if method_config.get('use_iupac', False) else 'false',
        snakelines_path = config['snakelines_dir']
    conda:
        config['snakelines_dir'] + '/enviroments/create_consensus_fasta.yaml'
    shell:
        """
        mask=''
        if (( {params.mask_lte_coverage} >= 0 )); then
            # coverage value is at column 4 of bedgraph
            bedtools merge -i <(awk '$4 > {params.mask_lte_coverage}' {input.bedgraph} | cut -f 1-3) > {params.coverage_bed}
            bedtools complement -i {params.coverage_bed} -g {input.genome} > {params.complement_bed}
            vcf2bed --deletions < {input.vcf} > {params.deletions_bed}
            bedtools subtract -a {params.complement_bed} -b {params.deletions_bed} > {params.masking_bed}
            mask='--mask {params.masking_bed}'
        fi

        bcftools consensus \
            --fasta-ref {input.fasta} \
            --output {output.fasta} \
            $mask \
            {input.vcf_gz} \
        1> {log.out} \
        2> {log.err}
        if {params.use_iupac}; then
            python3 {params.snakelines_path}/rules/consensus/fasta/add_iupac_codes.py {output.fasta} {input.vcf} {output.fasta}
        fi

        sed -i '1s/>.*/>{wildcards.sample}/' {output.fasta}
        """
