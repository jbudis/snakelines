rule qiime2__taxonomy_import_ref_reads:
    """
    Import reference reads to obtain the qza artifact.

    :input ref_reads: fasta reference file, e.g. reference/silva-16S/silva-16S.fa
    :output ref_reads: qiime artifact of imported fasta file
    """
    input:
        ref_reads = 'reference/{reference}/{reference}.fa'
    output:
        ref_reads = 'reference/{reference}/qiime2/ref_reads.qza'
    conda:
        config['snakelines_dir'] + '/enviroments/qiime2.yaml'
    shell:
        '''
        qiime tools import \
            --type 'FeatureData[Sequence]' \
            --input-path {input.ref_reads} \
            --output-path {output.ref_reads}
        '''


rule qiime2__taxonomy_import_ref_tax:
    """
    Import reference taxonomy to obtain the qza artifact.

    :input ref_tax: taxonomy reference file, e.g. reference/silva-16S/silva-16S.tax
    :output ref_tax: qiime artifact of imported reference taxonomy
    """
    input:
        ref_tax = 'reference/{reference}/{reference}.tax'
    output:
        ref_tax = 'reference/{reference}/qiime2/ref_tax.qza'
    conda:
        config['snakelines_dir'] + '/enviroments/qiime2.yaml'
    shell:
        '''
        qiime tools import \
            --type 'FeatureData[Taxonomy]' \
            --input-format HeaderlessTSVTaxonomyFormat \
            --input-path {input.ref_tax} \
            --output-path {output.ref_tax}
        '''

rule qiime2__taxonomy_fit_naive_bayes:
    """
    Create a scikit-learn naive_bayes classifier for reads.

    Calls 'qiime feature-classifier fit-classifier-naive-bayes'.
    
    :input ref_reads: qza artifact of imported reference sequences
    :input ref_tax: qza artifact of imported reference taxonomy
    :output classifier: qza artifact of trained classifier
    """
    input:
        ref_reads = 'reference/{reference}/qiime2/ref_reads.qza',
        ref_tax = 'reference/{reference}/qiime2/ref_tax.qza'
    output:
        classifier = 'reference/{reference}/qiime2/classifier.qza'
    conda:
        config['snakelines_dir'] + '/enviroments/qiime2.yaml'
    shell:
        '''
        qiime feature-classifier fit-classifier-naive-bayes \
            --i-reference-reads {input.ref_reads} \
            --i-reference-taxonomy {input.ref_tax} \
            --o-classifier {output.classifier} 
        '''