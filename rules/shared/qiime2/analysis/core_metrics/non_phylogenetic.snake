rule qiime2__diversity_core_metrics_nonphylogenetic:
    """
    Performs QIIME 2's diversity analyses through the q2-diversity plugin.

    Calls 'qiime diversity core-metrics'.

    Supports computing alpha and beta diversity metrics, applys related statistical tests, and generate interactive visualizations. First apply the core-metrics-phylogenetic method, which rarefies a FeatureTable[Frequency] to a user-specified depth, computes several alpha and beta diversity metrics, and generates principle coordinates analysis (PCoA) plots using Emperor for each of the beta diversity metrics. The metrics computed by default are:

    * Alpha diversity
        * Shannon's diversity index (a quantitative measure of community richness)
        * Observed OTUs (a qualitative measure of community richness)
        * Evenness (or Pielou's Evenness; a measure of community evenness)

    * Beta diversity
        * Jaccard distance (a qualitative measure of community dissimilarity)
        * Bray-Curtis distance (a quantitative measure of community dissimilarity)

    An important parameter for this script is --p-sampling-depth, which is the even sampling (i.e. rarefaction) depth. In this pipeline, sampling depth is set as Minimum of sequences count (e.g. we have 3 samples, first with 3500 sequences, 2nd with 3000 sequences and 3rd with 4500 sequences).

    Note:
    Because most diversity metrics are sensitive to different sampling depths across different samples, this script will randomly subsample the counts from each sample to the value provided for this parameter. For example, if you provide --p-sampling-depth 500, this step will subsample the counts in each sample without replacement so that each sample in the resulting table has a total count of 500. If the total count for any sample(s) are smaller than this value, those samples will be dropped from the diversity analysis. Choosing this value is tricky.

    :input rooted_tree: rooted tree, qiime artifact of type Phylogeny[Rooted]
    :input table: qiime artifact of type FeatureTable[Frequency]
    :input metadata: samples metadata filepath in TSV format (tab-separated value)
    :param sample_freqs_csv: sample_frequencies.csv obtained in report of input feature table, used for auto calculation of sampling depth

    :output rarefied_table: qiime artifact of type FeatureTable[Frequency]
    :output vector: sample data for alpha diversity, qiime artifact of type SampleData[AlphaDiversity]
    :output distance_matrix: distance matrix for beta diversity, qiime artifact of type DistanceMatrix
    :output emperor_plots: Emperor visualization of distance matrix

    """
    input:
        table       = 'qiime2/preprocess/denoise/%s/{reference}_table.qza' % pipeline.qiime2_post_taxa,
        metadata    = 'description/sample-metadata.tsv',
        denoise_stats = 'qiime2/preprocess/report/{reference}_table/'
    output:
        rarefied_table = 'qiime2/analysis/core_metrics/{reference}_rarefied_table.qza',
        observed_vector = 'qiime2/analysis/core_metrics/{reference}_observed_vector.qza',
        shannon_vector = 'qiime2/analysis/core_metrics/{reference}_shannon_vector.qza',
        evenness_vector = 'qiime2/analysis/core_metrics/{reference}_evenness_vector.qza',
        jaccard_dist_matrix = 'qiime2/analysis/core_metrics/{reference}_jaccard_dist_matrix.qza',
        bray_curtis_dist_matrix = 'qiime2/analysis/core_metrics/{reference}_bray_curtis_dist_matrix.qza',
        jaccard_pcoa = 'qiime2/analysis/core_metrics/{reference}_jaccard_pcoa.qza',
        bray_curtis_pcoa = 'qiime2/analysis/core_metrics/{reference}_bray_curtis_pcoa.qza',
        jaccard_emperor = 'qiime2/analysis/core_metrics/{reference}_jaccard_emperor.qzv',
        bray_curtis_emperor = 'qiime2/analysis/core_metrics/{reference}_bray_curtis_emperor.qzv'
    params:
        sampling_depth = str(method_config.get('sampling_depth','auto')),
        sample_freqs_csv = 'sample-frequency-detail.csv'
    threads:
        int(config['threads'])
    log:
        out = 'qiime2/analysis/core_metrics/log/{reference}_core_metrics.out',
        err = 'qiime2/analysis/core_metrics/log/{reference}_core_metrics.err'
    conda:
        config['snakelines_dir'] + '/environments/qiime2.yaml'
    shell:
        '''
        if [ {params.sampling_depth} = "auto" ]; then
            min="tail {input.denoise_stats}/{params.sample_freqs_csv} -n 1 | sed -E 's/.*,([0-9]*)\\..*/\\1/'";
            min=$(eval $min);
            if [[ $min -lt 1 ]]; then
                min=1;
            fi;
        else
            min={params.sampling_depth};
        fi;

        qiime diversity core-metrics \
            --i-table {input.table} \
            --p-sampling-depth $min \
            --p-n-jobs-or-threads {threads} \
            --m-metadata-file {input.metadata} \
            --o-rarefied-table {output.rarefied_table} \
            --o-observed-features-vector {output.observed_vector} \
            --o-shannon-vector {output.shannon_vector} \
            --o-evenness-vector {output.evenness_vector} \
            --o-jaccard-distance-matrix {output.jaccard_dist_matrix} \
            --o-bray-curtis-distance-matrix {output.bray_curtis_dist_matrix} \
            --o-jaccard-pcoa-results {output.jaccard_pcoa} \
            --o-bray-curtis-pcoa-results {output.bray_curtis_pcoa} \
            --o-jaccard-emperor {output.jaccard_emperor} \
            --o-bray-curtis-emperor {output.bray_curtis_emperor} \
            1> {log.out} \
            2> {log.err}
        '''
