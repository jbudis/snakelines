rule qiime2__taxonomy_barplot:
    """
    :input table:
    :input taxonomy: produced taxonomy as qza
    :input metadata: samples metadata filepath in TSV format (tab-separated value), e.g. description/silva16S-sample-metadata.tsv
    """
    input:
        taxonomy  = 'qiime2/dada2_experiment/taxonomy/{label}/{reference}_tax.qza',
        feature_table = 'qiime2/dada2_experiment/preprocess/{label}/{reference}_table.qza',
        metadata = 'description/sample-metadata.tsv'
    output:
        barplot = 'qiime2/dada2_experiment/taxonomy/{label}/{reference}_barplot.qzv'
    log:
        out = 'qiime2/dada2_experiment/taxonomy/{label}/log/{reference}_barplot.out',
        err = 'qiime2/dada2_experiment/taxonomy/{label}/log/{reference}_barplot.err',
    conda:
        config['snakelines_dir'] + '/environments/qiime2.yaml'
    shell:
        '''
        qiime taxa barplot \
            --i-table {input.feature_table} \
            --i-taxonomy {input.taxonomy} \
            --m-metadata-file {input.metadata} \
            --o-visualization {output.barplot} \
            1> {log.out} \
            2> {log.err}
        '''

rule qiime2__taxonomy_tabulate:
    """
    Visualize produced taxonomy classification

    Calls 'qiime metadata tabulate'

    :input taxonomy: produced taxonomy as qza
    :output qzv: visualization of taxa features in qzv
    """
    input:
        taxonomy  = 'qiime2/dada2_experiment/taxonomy/{label}/{reference}_tax.qza',
    output:
        qzv = 'qiime2/dada2_experiment/taxonomy/{label}/{reference}_features.qzv'
    log:
        out = 'qiime2/dada2_experiment/taxonomy/{label}/log/{reference}_features.out',
        err = 'qiime2/dada2_experiment/taxonomy/{label}/log/{reference}_features.err',
    conda:
        config['snakelines_dir'] + '/environments/qiime2.yaml'
    shell:
        '''
        qiime metadata tabulate \
            --m-input-file {input.taxonomy} \
            --o-visualization {output.qzv} \
            1> {log.out} \
            2> {log.err}
        '''
