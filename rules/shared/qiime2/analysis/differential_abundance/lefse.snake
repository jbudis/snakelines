rule qiime2__taxonomy_collapse:
    """
    :input table: feature table as produced by dada2
    :input taxonomy: produced taxonomy as qza
    :output taxa_collapsed: feature table collapsed at specified taxa level 
    """
    input:
        feature_table = 'reads/preprocess/denoise/{reference}_table.qza',
        taxonomy = 'reads/taxonomy/{reference}_tax.qza'
    output:
        taxa_collapsed = 'reads/taxonomy/{reference}_tax_collapsed.qza'
    params:
        level = method_config.get("taxa_level",6)
    conda:
        config['snakelines_dir'] + '/enviroments/qiime2.yaml'
    shell:
        ''' 
        echo Level is {params.level}   
        qiime taxa collapse \
            --i-table {input.feature_table} \
            --o-collapsed-table {output.taxa_collapsed} \
            --p-level {params.level} \
            --i-taxonomy {input.taxonomy}
        '''

rule qiime2__taxonomy_convert_to_relative_frequency:
    """
    :input taxa_collapsed: feature table collapsed at specified taxa level
    :output taxa_collapsed_relative: the same input feature table, but frequencies converted to relative
    """
    input:
        taxa_collapsed = 'reads/taxonomy/{reference}_tax_collapsed.qza'
    output:
        taxa_collapsed_relative = 'reads/taxonomy/lefse/{reference}_tax_collapsed_relative.qza'
    conda:
        config['snakelines_dir'] + '/enviroments/qiime2.yaml'
    shell:
        '''    
        qiime feature-table relative-frequency \
            --i-table {input.taxa_collapsed} \
            --o-relative-frequency-table {output.taxa_collapsed_relative}
        '''

rule qiime2__report_export_qza:
    """
    Exports feature table with relative frequencies from qza into BIOM format
    :input qza: qiime artifact
    :output biom_file: feature table 
    """
    input:
        qza = 'reads/taxonomy/lefse/{reference}_tax_collapsed_relative.qza'
    output:
        biom_file = 'reads/taxonomy/lefse/{reference}_tax_collapsed_relative/feature-table.biom',
        export_dir = directory('reads/taxonomy/lefse/{reference}_tax_collapsed_relative/')
    conda:
        config['snakelines_dir'] + '/enviroments/qiime2.yaml'
    shell:
        '''
        qiime tools export \
            --input-path {input.qza} \
            --output-path {output.export_dir}
        '''

rule qiime2__biom_convert_to_txt:
    """
    Converts feature table from biom format into tsv and removes redundant BIOM header
    :input qza: qiime artifact
    :output tsv_file: feature table with taxas and its relative frequencies in .tsv format
    """
    input:
        biom_file = 'reads/taxonomy/lefse/{reference}_tax_collapsed_relative/feature-table.biom'
    output:
        tsv_file = 'reads/taxonomy/lefse/{reference}_tax_collapsed_relative/feature-table.tsv'
    params:
        txt_file = 'reads/taxonomy/lefse/{reference}_tax_collapsed_relative/feature-table.txt'
    conda:
        config['snakelines_dir'] + '/enviroments/biom_format.yaml'
    shell:
        '''
        biom convert \
            -i {input.biom_file} \
            -o {params.txt_file} \
            --to-tsv;
        
        tail -n +2 {params.txt_file} >> {output.tsv_file};
        rm {params.txt_file};
        '''

rule qiime2_prepare_biom_convert:
    """
    Format converted biom file in .tsv to obtain table with metadata information
    Taxas are rewritten using | instead of ; and also empty taxas are handled

    :input tsv_file: feature table with taxas and its relative frequencies in .tsv format
    :input metadata: samples metadata filepath in TSV format (tab-separated value), e.g. description/silva16S-sample-metadata.tsv
    :output tsv_file: modified input feature table with metadata headers and reformatted taxas as required for LEFse
    """
    input:
        tsv_file = 'reads/taxonomy/lefse/{reference}_tax_collapsed_relative/feature-table.tsv',
        metadata = 'description/joined-sample-metadata.tsv'
    output:
        tsv_file = 'reads/taxonomy/lefse/{reference}_tax_collapsed_relative/feature-table_corr.tsv'
    params:
        script = srcdir("lefse_prepare.py"),
        class_col = method_config.get("class_col",None),
        subclass_col = None,#'phase',
        subject_col = None
    shell:
        '''
        python {params.script} \
            {input.tsv_file} \
            {input.metadata} \
            {params.class_col} \
            {params.subclass_col} \
            {params.subject_col} \
            {output.tsv_file}
        '''

rule qiime2__prepare_lefse_input:
    """
    Convert the input data matrix to the format for LEfSe.

    Calls 'lefse_format_input.py'.

    :input prepared_table: Formatted feature table with taxa collapsed and given abundances
    :output lefse_input: Converted input to LEFse input format
    :output lefse_input_tsv: Converted input to LEFse input format and exported to .tsv
    """
    input:
        prepared_table = 'reads/taxonomy/lefse/{reference}_tax_collapsed_relative/feature-table_corr.tsv'
    output:
        lefse_input = 'reads/lefse/{reference}.in',
        lefse_input_tsv = 'reads/lefse/{reference}.tsv'
    conda:
        config['snakelines_dir'] + '/enviroments/lefse_qiime2.yaml'
    shell:
        '''
        lefse_format_input.py \
            {input.prepared_table} \
            {output.lefse_input} \
            -c 1 -u 2 -o 1000000 \
            --output_table {output.lefse_input_tsv}
        '''

rule qiime2__run_lefse:
    """
    Performs the actual statistical analysis as given in LEFse

    Calls 'lefse_run.py'.

    :input lefse_input: Input taxonomy abundances in LEFse input format
    :output lefse_result: Results of LEFse statistical analysis, in LEFse unique format
    """
    input:
        lefse_input = 'reads/lefse/{reference}.in'
    output:
        lefse_result = 'reads/lefse/{reference}.res'
    conda:
        config['snakelines_dir'] + '/enviroments/lefse_qiime2.yaml'
    shell:
        '''
        lefse_run.py \
            {input.lefse_input} \
            {output.lefse_result}
        '''

rule qiime2__pdf_lefse_result:
    """
    Visualizes the Lefse statistical results in .pdf format.
    Plot the list of biomarkers with their effect size.

    Calls 'lefse_plot_res.py'.

    :input lefse_result: Results of LEFse statistical analysis, in LEFse unique format
    :output lefse_result_pdf: Results of LEFse statistical analysis, in PDF format
    """
    input:
        lefse_result = 'reads/lefse/{reference}.res'
    output:
        lefse_result_pdf = 'reads/lefse/{reference}.pdf'
    conda:
        config['snakelines_dir'] + '/enviroments/lefse_qiime2.yaml'
    shell:
        '''
        lefse_plot_res.py \
            {input.lefse_result} \
            {output.lefse_result_pdf} \
            --format pdf
        '''

rule qiime2__lefse_cladogram:
    """
    Visualizes the output on a hierarchical tree
    Plot the representation of the biomarkers on the hierarchical tree

    Calls 'lefse_plot_cladogram.py'.

    :input lefse_result: Results of LEFse statistical analysis, in LEFse unique format
    :output cladogram_out: hierarchical tree representation of the biomarkers, in .pdf format
    """
    input:
        lefse_result = 'reads/lefse/{reference}.res'
    output:
        cladogram_out = 'reads/lefse/{reference}.cladogram.pdf'
    conda:
        config['snakelines_dir'] + '/enviroments/lefse_qiime2.yaml'
    shell:
        '''
        lefse_plot_cladogram.py \
            {input.lefse_result} \
            {output.cladogram_out} \
            --format pdf
        '''

rule qiime2__lefse_plot_features:
    """
    Visualizes the raw-data features. The module for exporting the raw-data representation of the features.

    Calls 'lefse_plot_features.py'.

    :input lefse_input: Input taxonomy abundances in LEFse input format
    :input lefse_result: Results of LEFse statistical analysis, in LEFse unique format
    :output lefse_out_dir: directory where the raw-data representation of the discovered biomarkers will be stored
    """
    input:
        lefse_input = 'reads/lefse/{reference}.in',
        lefse_result = 'reads/lefse/{reference}.res'
    output:
        lefse_out_dir = directory('reads/lefse/{reference}_features/')
    conda:
        config['snakelines_dir'] + '/enviroments/lefse_qiime2.yaml'
    shell:
        '''
        lefse_plot_features.py \
            {input.lefse_input} \
            {input.lefse_result} \
            {output.lefse_out_dir}
        '''