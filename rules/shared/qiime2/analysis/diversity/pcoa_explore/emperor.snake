rule qiime2__emperor__plot:
    """
    Creates visualization for time series data. Generates Emperor plots so that the resulting plot will contain axes for principal coordinate 1, principal coordinate 2, and custom axes column. It will use that last axis to explore how these samples changed over time.

    Calls 'qiime emperor plot'.

    :input pcoa_results: principal coordinates, qiime artifact of type PCoAResults
    :input metadata: samples' metadata filepath in TSV format (tab-separated value)
    :output qzv: qiime visualization

    Note: column used for custom axes must be numerical, not categorical.
    """
    input:
        pcoa_results = 'qiime2/analysis/core_metrics/{reference}_{type}_pcoa.qza',
        metadata = 'description/sample-metadata.tsv'
    output:
        qzv = 'qiime2/analysis/core_metrics/pcoa_explore/{reference}_{type}_{column}_emperor.qzv'
    log:
        out = 'qiime2/analysis/core_metrics/log/{reference}_{type}_{column}_emperor.out',
        err = 'qiime2/analysis/core_metrics/log/{reference}_{type}_{column}_emperor.err'
    conda:
        config['snakelines_dir'] + '/environments/qiime2.yaml'
    shell:
        '''
        qiime emperor plot \
            --i-pcoa {input.pcoa_results} \
            --m-metadata-file {input.metadata} \
            --p-custom-axes {wildcards.column} \
            --o-visualization {output.qzv} \
            1> {log.out} \
            2> {log.err}
        '''
