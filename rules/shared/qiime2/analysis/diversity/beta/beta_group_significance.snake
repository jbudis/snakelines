rule qiime2__diversity__beta_group_significance:
    """
    Creates visualization of sample composition in the context of categorical metadata using PERMANOVA (first described in Anderson (2001)).

    Calls 'qiime diversity beta-group-significance'.

    :input distance_matrix: distance matrix for beta diversity, qiime artifact of type DistanceMatrix, e.g. reads/qiime/silva16S-core-metrics-results-dn-99/bray_curtis_distance_matrix.qza
    :input metadata: samples' metadata filepath in TSV format (tab-separated value), e.g. description/silva16S-sample-metadata.tsv
    :output qzv: qiime visualization, e.g. reads/qiime/silva16S-core-metrics-results-dn-99/beta/bray_curtis_col4_significance.qzv
    """
    input:
        dist_matrix = 'reads/analysis/core_metrics/{reference}_{type}_dist_matrix.qza',
        metadata = 'description/sample-metadata.tsv'
    output:
        qzv = 'reads/analysis/core_metrics/beta/{reference}_{type}_{column}_significance.qzv',
    log:
        out = 'reads/analysis/core_metrics/log/{reference}_{type}_{column}_beta.out',
        err = 'reads/analysis/core_metrics/log/{reference}_{type}_{column}_beta.err' 
    conda:
        config['snakelines_dir'] + '/environments/qiime2.yaml'
    params:
        pairwise = '--p-pairwise' if method_config.get('pairwise_tests',True) else '--p-no-pairwise',
        test_method = method_config.get('test_method','permanova')
    shell:
        '''
        qiime diversity beta-group-significance \
            --i-distance-matrix {input.dist_matrix} \
            --m-metadata-file {input.metadata} \
            --m-metadata-column {wildcards.column} \
            --o-visualization {output.qzv} \
            {params.pairwise} \
            --p-method {params.test_method} \
            1> {log.out} \
            2> {log.err}
        '''
