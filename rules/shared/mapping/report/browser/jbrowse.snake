include: config['snakelines_dir'] + '/rules/shared/browser/jbrowse/merge.snake'

rule bamcoverage__create_coverage_track:
    input:
        bam = 'mapping/{reference}/sorted/{sample}.bam',
        bai = 'mapping/{reference}/sorted/{sample}.bam.bai'
    output:
        bw   = 'mapping/{reference}/jbrowse/coverages/{sample}.bw'
    log:
        out = 'mapping/{reference}/jbrowse/log/{sample}/bamcoverage.out',
        err = 'mapping/{reference}/jbrowse/log/{sample}/bamcoverage.err'
    conda:
        config['snakelines_dir'] + '/enviroments/deeptools.yaml' 
    shell:
        """
        bamCoverage \
            -b {input.bam} \
            -o {output.bw} \
        > {log.out} \
        2> {log.err}
        """

def all_coverage_tracks(wildcards):
    return expand('mapping/{reference}/jbrowse/coverages/{sample}.bw',
        reference=wildcards.reference, sample=pipeline.samples_for(wildcards.reference))


rule summarize_configs:
    input:
        coverages = all_coverage_tracks
    output:
        conf = 'mapping/{reference}/jbrowse/coverage_tracks.conf'
    shell:
        """
        for BW in {input.coverages}; do
            SID=$(basename $BW | cut -f1 -d '.')
            fullpath=coverages/$(basename $BW)

            echo "[tracks.coverage_${{SID}}]" >> {output.conf}
            echo "category        = Alignments" >> {output.conf}
            echo "key             = $SID" >> {output.conf}
            echo "urlTemplate     = $fullpath" >> {output.conf}
            echo "storeClass      = JBrowse/Store/BigWig" >> {output.conf}
            echo "type            = JBrowse/View/Track/Wiggle/Density" >> {output.conf}
            echo "" >> {output.conf}
        done
        """
ruleorder: bamcoverage__create_coverage_track > summarize_configs > create_final_config
