# TODO better comments for Bismark reports
rule bismark__methylation_extractor:
    """
    Reads in a bisulfite read alignment results file produced by the Bismark bisulfite mapper and extracts the
    methylation information for individual cytosines
    :input bam: mapped reads with Bismark in BAM format
    :input ct_index: CT reference index (created by rule bismark__prepare_index)
    :input ga_index: GA reference index (created by rule bismark__prepare_index)
    :input ref: reference genome, i.e. reference/{reference}/{reference}.fa
    :output mbias_report: report generated by the bismark_methylation_extractor tool
    :output splitting_report: report generated by the bismark_methylation_extractor tool
    :params out_dir: directory for output files
    :params ref_dir: directory with the reference genome and Bismark index
    """
    input:
        bam                  = 'mapping/{reference}/original/{sample}.bam',
        ct_index             = 'reference/{reference}/Bisulfite_Genome/CT_conversion/BS_CT.1.bt2',
        ga_index             = 'reference/{reference}/Bisulfite_Genome/GA_conversion/BS_GA.1.bt2',
        ref                  = 'reference/{reference}/{reference}.fa'
    output:
        mbias_report         = 'mapping/{reference}/original/bismark/{sample}.M-bias.txt',
        splitting_report     = 'mapping/{reference}/original/bismark/{sample}_splitting_report.txt'
    params:
        out_dir              = 'mapping/{reference}/original/bismark',
        ref_dir              = 'reference/{reference}',
    threads:
        int(config['threads'])
    log:
        out                  = 'mapping/{reference}/original/bismark/log/{sample}.methylation_extractor.log',
        err                  = 'mapping/{reference}/original/bismark/log/{sample}.methylation_extractor.err'
    conda:
        config['snakelines_dir'] + '/enviroments/bismark.yaml'
    shell:
        '''
        bismark_methylation_extractor \
            --paired-end \
            --parallel {threads} \
            --output {params.out_dir} \
            --genome_folder {params.ref_dir} \
            {input.bam} \
        >  {log.out} \
        2> {log.err}
        '''

rule bismark__summary_report:
    """
    Aggregate results from several methylation statistic files generated by Bismark into single summary HTML report
    :input alignment_report: Report generated with Bismark together with read alignment
    :input splitting_report: Report generated by the bismark_methylation_extractor tool
    :input mbias_report: Report generated by the bismark_methylation_extractor tool
    :output html: Aggregated quality report of discovered methylation
    """
    input:
        alignment_report = 'mapping/{reference}/original/bismark/{sample}-alignment_report.txt',
        mbias_report     = 'mapping/{reference}/original/bismark/{sample}.M-bias.txt',
        splitting_report = 'mapping/{reference}/original/bismark/{sample}_splitting_report.txt'
    output:
        html             = 'mapping/{reference}/original/bismark/{sample}.html'
    log:
        out              = 'mapping/{reference}/original/bismark/log/{sample}.html.log',
        err              = 'mapping/{reference}/original/bismark/log/{sample}.html.err'
    conda:
        config['snakelines_dir'] + '/enviroments/bismark.yaml'
    shell:
        '''
        bismark2report \
            --output {output.html} \
            --alignment_report {input.alignment_report} \
            --splitting_report {input.splitting_report} \
            --mbias_report {input.mbias_report} \
        >  {log.out} \
        2> {log.err}
        '''
