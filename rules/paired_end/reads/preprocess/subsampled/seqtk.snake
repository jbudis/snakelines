rule seqtk__subsample_reads:
    """
    Randomly select user-configured number of reads from fastq files
    :input r1: Left side of sequenced fragments in gzipped fastq format
    :input r2: Right side of sequenced fragments in gzipped fastq format
    :output r1: Left side of subsampled fragments without overlap in gzipped fastq format
    :output r2: Right side of subsampled fragments without overlap in gzipped fastq format
    :params seed: Number for initiation of pseudo-randomized selection of reads. Runs with the same seed would pick the same reads.
    :params n_reads: Number of reads to select
    """
    input:
        r1 = 'reads/%s/{sample}_R1.fastq.gz' % method_config['input_read_type'],
        r2 = 'reads/%s/{sample}_R2.fastq.gz' % method_config['input_read_type']
    output:
        r1 = configured_temp('reads/subsampled/{sample}_R1.fastq.gz'),
        r2 = configured_temp('reads/subsampled/{sample}_R2.fastq.gz')
    params:
        seed = method_config.get('seed', 1),
        n_reads = method_config['n_reads']
    conda:
        config['snakelines_dir'] + '/enviroments/seqtk.yaml'
    shell:
        """
        seqtk sample -s {params.seed} -2 {input.r1} {params.n_reads} | gzip > {output.r1}
        seqtk sample -s {params.seed} -2 {input.r2} {params.n_reads} | gzip > {output.r2}
        """