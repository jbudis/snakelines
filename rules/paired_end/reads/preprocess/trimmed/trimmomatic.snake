def get_illuminaclip():
    illuminaclip_list = []
    if 'illuminaclip' in method_config:
        illuminaclip_params = method_config.get('illuminaclip')
        illuminaclip_list.append('ILLUMINACLIP')
        illuminaclip_list.append('description/adapters/%s.fa' % illuminaclip_params.get('fasta'))
        illuminaclip_list.append(illuminaclip_params.get('seed_mismatches'))
        illuminaclip_list.append(illuminaclip_params.get('palindrome_clip_threshold'))
        illuminaclip_list.append(illuminaclip_params.get('simple_clip_threshold'))
        if 'min_adapter_length' in illuminaclip_params:
            illuminaclip_list.append(illuminaclip_params.get('min_adapter_length'))
        if 'keep_both_reads' in illuminaclip_params:
            illuminaclip_list.append(illuminaclip_params.get('keep_both_reads'))

    return ':'.join(map(str, illuminaclip_list)) if illuminaclip_list else ''

rule trimmomatic__trim_reads:
    """
    Remove low quality ends of reads and then filter reads that are too short.
    :input r1: Left side of sequenced fragments in gzipped fastq format
    :input r2: Right side of sequenced fragments in gzipped fastq format
    :output r1: Left side of subsampled fragments without overlap in gzipped fastq format
    :output r2: Right side of subsampled fragments without overlap in gzipped fastq format
    :params r1: Temporary unzipped left side of sequenced fragments in gzipped fastq format
    :params r2: Temporary unzipped right side of sequenced fragments in gzipped fastq format
    :params adapter: Parameters of Trimmomatic for adapter clipping
    :params quality: Parameters of Trimmomatic for clipping of low quality 3` ends of reads
    :params headcrop: Parameters of Trimmomatic for cutting of pre-selected number of bases from the 5` end of reads
    :params crop: Parameters of Trimmomatic for cutting of pre-selected number of bases from the 3` end of reads
    :params minlen: Parameters of Trimmomatic to remove reads that have lower length after trimming
    """
    input:
        r1 = 'reads/%s/{sample}_R1.fastq.gz' % method_config['input_read_type'],
        r2 = 'reads/%s/{sample}_R2.fastq.gz' % method_config['input_read_type']
    output:
        r1 = configured_temp('reads/trimmed/{sample}_R1.fastq.gz'),
        r2 = configured_temp('reads/trimmed/{sample}_R2.fastq.gz')
    params:
        r1 = 'reads/trimmed/{sample}_R1.fastq',
        r2 = 'reads/trimmed/{sample}_R2.fastq',
        additional = method_config.get('additional_params', ''),
        quality = 'SLIDINGWINDOW:5:%s' % method_config['quality'] if 'quality' in method_config else '',
        headcrop = 'HEADCROP:%s' % method_config['headcrop'] if 'headcrop' in method_config else '',
        crop = 'CROP:%s' % method_config['crop'] if 'crop' in method_config else '',
        minlen = 'MINLEN:%s' % method_config['minlen'] if 'minlen' in method_config else '',
        illuminaclip = get_illuminaclip()
    threads:
        int(config['threads'])
    log:
        out = 'reads/trimmed/log/{sample}.log',
        err = 'reads/trimmed/log/{sample}.err'
    conda:
        config['snakelines_dir'] + '/enviroments/trimmomatic.yaml'
    shell:
        """
        trimmomatic PE \
            -threads {threads} \
            -phred33 \
            {input.r1} \
            {input.r2} \
            {output.r1} /dev/null \
            {output.r2} /dev/null \
            {params.additional} \
            {params.crop} \
            {params.headcrop} \
            {params.quality} \
            {params.minlen} \
            {params.illuminaclip} \
        >  {log.out} \
        2> {log.err}
        """

