rule qiime2__import_manifest:
    """
    Imports samples written down in metadata file, sequences should NOT be pair-end. Calls 'qiime tools import'
    :input manifest: TSV manifest file in format 'sample-id,absolute-filepath,direction', e.g. reads/qiime/silva16S.manifest
    :output qza: qiime artifact of type SampleData[{params.data_type}], e.g. reads/qiime/silva16S-imported.qza
    :params data_type: SampleData type, possible values: SequencesWithQuality
    :params input_format: defines PHRED offset in quality scores used in samples
    """
    input:
        manifest = 'reads/import/{reference}.manifest'
    output:
        qza = 'reads/import/{reference}.qza'
    log:
        out = 'reads/import/log/{reference}.log',
        err = 'reads/import/log/{reference}.err'
    params:
        sequence_type = "'SampleData[SequencesWithQuality]'",
        input_format = 'SingleEndFastqManifestPhred' + str(method_config.get('PHRED_offset',33)) + 'V2'
    conda:
        config['snakelines_dir'] + '/environments/qiime2.yaml'
    shell:
        '''
        qiime tools import \
            --type {params.sequence_type} \
            --input-path {input.manifest} \
            --output-path {output.qza} \
            --input-format {params.input_format} \
            1> {log.out} \
            2> {log.err}
        '''

rule copy_description_metadata:
    """
    Copies used description metadata
    """
    input:
        metadata = 'description/sample-metadata.tsv'
    output:
        metadata = 'reads/import/sample-metadata.tsv'
    shell:
        '''
        cp {input.metadata} {output.metadata}
        '''
