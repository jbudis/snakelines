shell.executable('bash')

import re

include: config['snakelines_dir'] + '/rules/preprocess/conversion/unzip.snake'
include: config['snakelines_dir'] + '/rules/mapping/mappers/bowtie.snake'

rule emirge_prepare_reference:
    input:
        fa = 'reference/{reference}/{reference}.fa'
    output:
        fa = 'reference/{reference}/{reference}.emirge.fa'
    shell:
        '''
        /usr/local/tools/emirge-0.61.0/utils/fix_nonstandard_chars.py \
            < {input.fa} \
            > {output.fa}
        '''

rule uchime_prepare_reference:
    input:
        fa = 'reference/{reference}/{reference}.fa'
    output:
        fa = 'reference/{reference}/{reference}.gold.fa'
    shell:
        '''
        ln -s `basename {input.fa}` {output.fa}
        '''

rule emirge_classification:
    input:
        r1 = 'reads/%s/{sample}_R1.fastq' % config['emirge']['read_type'],
        r2 = 'reads/%s/{sample}_R2.fastq' % config['emirge']['read_type'],
        reffasta = 'reference/{reference}/{reference}.emirge.fa',
        refindex = 'reference/{reference}/bowtie_index/{reference}.emirge.1.ebwt'
    output:
        bam = 'emirge/{reference}/iterations/{sample}/iter.40/bowtie.iter.40.PE.bam'
    params:
        refindex = 'reference/{reference}/bowtie_index/{reference}.emirge',
        emirge_dir = 'emirge/{reference}/iterations/{sample}'
    log:
        'emirge/{reference}/iterations/{sample}/log/emirge.log'
    threads:
        int(config['threads'])
    shell:
        '''
        emirge \
            {params.emirge_dir} \
            -1 {input.r1} \
            -2 {input.r2} \
            --fasta_db {input.reffasta} \
            --bowtie_db {params.refindex} \
            --max_read_length 35 \
            --insert_mean 300 \
            --insert_stddev 1000 \
            --processors {threads} \
            --phred33 \
        > {log}
        '''

rule extract_emirge_results:
    input:
        bam = 'emirge/{reference}/iterations/{sample}/iter.40/bowtie.iter.40.PE.bam'
    output:
        temporary('emirge/{reference}/iterations/{sample}/{sample}.tmp')
    params:
        final_dir = 'emirge/{reference}/iterations/{sample}/iter.40'
    shell:
        '''
        emirge_rename_fasta \
            {params.final_dir} \
        > {output}
        '''


rule append_read_counts:
    input:
        tmp = 'emirge/{reference}/iterations/{sample}/{sample}.tmp'
    output:
        fa = 'emirge/{reference}/iterations/{sample}/{sample}.fa'
    params:
        final_bam = 'emirge/{reference}/iterations/{sample}/iter.40/bowtie.iter.40.PE.bam'
    run:
        import subprocess

        # extract read count
        result = subprocess.check_output('samtools view %s | cut -f 1 | uniq | wc -l' % params.final_bam, shell=True)
        read_count = int(result.strip())

        # add it to the fasta
        with open(input.tmp) as file_in, open(output.fa, 'w') as file_out:
            for line in file_in:
                if line.startswith('>'):
                    line = line.strip() + ' ReadCount=%d\n' % read_count
                file_out.write(line)


rule find_chimeras_uchime:
    input:
        reffasta = 'reference/{reference}/{reference}.gold.fa',
        seqs = 'emirge/{reference}/iterations/{sample}/{sample}.fa'
    output:
        summary = 'emirge/{reference}/iterations/{sample}/{sample}.uchime',
        alignments = 'emirge/{reference}/iterations/{sample}/{sample}.alns'
    log:
        'emirge/{reference}/iterations/{sample}/log/uchime.log'
    shell:
        '''
        uchime \
            --input {input.seqs} \
            --db {input.reffasta} \
            --uchimeout {output.summary} \
            --uchimealns {output.alignments} \
        > {log}
        '''

rule prepare_nonchimeric_for_krona:
    input:
        tax = 'reference/{reference}/{reference}.tax',
        summary = 'emirge/{reference}/iterations/{sample}/{sample}.uchime',
    output:
        krona_txt = 'emirge/{reference}/report/krona/individual/{sample}.krn'
    run:
        import os

        taxonomies = {line.split()[0]: line.strip().split('\t')[1].replace(';', '\t') for line in open(input.tax)}
        taxcopy = re.compile('.*_m\d\d?\d?')

        with open(input.summary) as summary, open(output.krona_txt, 'w') as krona:
            res = []
            for line in summary:
                params = line.strip().split('\t')
                if params[-1] == 'Y':
                    continue
                info = params[1][params[1].find('|')+1:].split()
                tabund = float(info[1].split('=')[1])
                read_count = int(info[-1].split('=')[1])

                tid = info[0]
                if taxcopy.match(tid):
                    tid = tid[:tid.rfind('_m')]

                taxes = taxonomies[tid]
                krona.write('%s\t%s\n'%(str(int(read_count*tabund + 0.49)), taxes))

rule emirge_krona_report:
    input:
        'emirge/{reference}/report/krona/individual/{sample}.krn'
    output:
        'emirge/{reference}/report/krona/individual/{sample}.html'
    log:
        'emirge/{reference}/report/krona/individual/log/{sample}.krona'
    shell:
        '''
        ktImportText \
            -o {output} \
            {input} \
        > {log}
        '''
