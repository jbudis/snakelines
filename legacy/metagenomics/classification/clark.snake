include: config['snakelines_dir'] + '/rules/preprocess/conversion/unzip.snake'

read_type = config['clark']['read_type']
db_version = config['clark']['db_version']
mode = config['clark']['mode']
kmer = config['clark']['kmer']
db_path = '/data/genome/metagenome/clark/' + db_version

rule clark:
    input:
        r1 = 'reads/%s/{sample}_R1.fastq' % read_type,
        r2 = 'reads/%s/{sample}_R2.fastq' % read_type
    output:
        'clark/classification/{sample}/reads.csv'
    params:
        prefix = 'clark/classification/{sample}/reads',
        db_path = db_path,
        mode = mode,
        kmer = kmer
    threads:
        int(config['threads'])
    shell:
        '''
        clark \
            -k {params.kmer} \
            -T {params.db_path}/targets.txt \
            -D {params.db_path} \
            -P {input.r1} \
               {input.r2} \
            -m {params.mode} \
            -R {params.prefix} \
            --extended \
            -n {threads}
        '''

rule clark_summary:
    input:
        'clark/classification/{sample}/reads.csv'
    output:
        'clark/classification/{sample}/summary.csv'
    params:
        prefix = 'clark/classification/{sample}/reads',
        db_path = db_path
    shell:
        '''
        /usr/local/tools/clark-1.2.3/estimate_abundance.sh \
            -F {input} \
            -D {params.db_path} \
        > {output}
        '''

rule krona_all_txt:
    input:
        'clark/classification/{sample}/summary.csv'
    output:
        'clark/report/krona/individual/{sample}.krn'
    shell:
        '''
        awk \
            -F, \
            '!/UNKNOWN/ {{ \
                gsub(";", "\t", $3); \
                print $4 "\t" $3 "\t" $1\
            }}' \
            {input} \
        | tail \
            -n +2 \
        > {output}
        '''

