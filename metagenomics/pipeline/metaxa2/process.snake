configfile: srcdir('config.yaml')

pipeline = glob_wildcards('reads/original/{sample, %s}_R1.fastq.gz' % config['sample_pattern'])

include: config['snake_dir'] + '/preprocess/trim_reads/trimmomatic.snake'
include: config['snake_dir'] + '/preprocess/report/fastqc.snake'
include: config['snake_dir'] + '/metagenomics/classification/metaxa2.snake'
include: config['snake_dir'] + '/metagenomics/report/krona.snake'
include: config['snake_dir'] + '/metagenomics/report/tsv_summary.snake'

genes = list(config['gene_pattern'].keys())
tax_levels = ['phylum', 'class', 'order', 'family', 'genus', 'species']
count_types = ['abs', 'rel']
report_dir = 'report/' + config['report_dir']

rule process:
    input:
        fastqc_original = 'reads/original/stats/summary.html',
        fastqc_trimmed  = 'reads/trimmed/stats/summary.html',
        krona    = expand('metaxa2/{gene}/report/krona/krona.html', gene=genes),
        summary  = expand('metaxa2/{gene}/report/tsv/subtaxes.tsv', gene=genes),
        tables   = expand('metaxa2/{gene}/report/tsv/{tax_level}.{count_type}.tsv', gene=genes, tax_level=tax_levels, count_type=count_types),
        barplots = expand('metaxa2/{gene}/report/figures/{tax_level}.{count_type}.png', gene=genes, tax_level=tax_levels, count_type=count_types)
    output:
        krona    = expand('{report_dir}/{gene}/krona/krona.html', gene=genes, report_dir=report_dir),
        summary  = expand('{report_dir}/{gene}/tsv/subtaxes.tsv', gene=genes, report_dir=report_dir),
        tables   = expand('{report_dir}/{gene}/tsv/{tax_level}.{count_type}.tsv', gene=genes, tax_level=tax_levels, count_type=count_types, report_dir=report_dir),
        barplots = expand('{report_dir}/{gene}/figures/{tax_level}.{count_type}.png', gene=genes, tax_level=tax_levels, count_type=count_types, report_dir=report_dir)
    params:
        genes = genes,
        report_dir = report_dir
    shell:
        '''
        for GENE in {params.genes}; do
            REPORT_DIR={params.report_dir}/$GENE
            cp -R metaxa2/$GENE/report/* $REPORT_DIR
        done
        '''
